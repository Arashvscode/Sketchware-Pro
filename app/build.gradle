plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    def gitCommitHash = 'git rev-parse HEAD'.execute().text.trim()

    def getCommitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    defaultConfig {
        applicationId 'com.sketchware.remod'
        //noinspection ExpiredTargetSdkVersion since we don't target getting Sketchware Pro on Google Play.
        targetSdkVersion 28
        versionCode 150
        versionName 'v6.4.0-SNAPSHOT-' + getCommitHash()

        buildConfigField("String", "GIT_HASH", "\"${gitCommitHash}\"")

        buildConfigField("String", "FLAVOR_NAME_WITH_AABS", "\"withAabOutputSupport\"")
        buildConfigField("String", "FLAVOR_NAME_WITHOUT_AABS", "\"withoutAabOutputSupport\"")
    }

    flavorDimensions 'api'
    productFlavors {
        withAabOutputSupport {
            dimension 'api'
            minSdkVersion 26
            versionNameSuffix " without AAB support"
        }

        withoutAabOutputSupport {
            dimension 'api'
            minSdkVersion 21
            versionNameSuffix " with AAB support"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    androidResources {
        additionalParameters '--stable-ids', 'public-stable-ids.txt'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar'])
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.tools:annotations:30.1.3'
    withAabOutputSupportImplementation('com.android.tools.build:bundletool:1.9.1') {
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'com.google.guava', module: 'guava'
    }

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}
